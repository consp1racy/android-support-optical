import com.android.build.gradle.LibraryExtension
import com.android.build.gradle.LibraryPlugin

buildscript {
    ext.kotlin_version = '1.3.21'

    repositories {
        google()
        jcenter()
        maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.3.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'digital.wup:android-maven-publish:3.6.2'
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
    }

    // Disable BuildConfig generation for Android library modules so javac can be skipped altogether.
    plugins.whenPluginAdded {
        if (it instanceof LibraryPlugin) {
            extensions.getByType(LibraryExtension).libraryVariants.all {
                // TODO replace with https://issuetracker.google.com/issues/72050365 once released.
                it.generateBuildConfig.enabled = false
            }
        }
    }

    group = "net.xpece.androidx.optical"
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
